
&НаКлиенте
Процедура СрокКредитаПриИзменении(Элемент)
	ОбновитьКоэффициентАннуитета();
	ОчиститьДанныеДопПлатежей();
	ПерезаполнитьРасчетнуюТаблицу();
КонецПроцедуры

&НаКлиенте
Процедура СтавкаКредитованияПриИзменении(Элемент)
	ОбновитьКоэффициентАннуитета();
	ОчиститьДанныеДопПлатежей();
	ПерезаполнитьРасчетнуюТаблицу();
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьКоэффициентАннуитета()
	КоэффициентАннуитета = КоэффициентАннуитета(ЭтотОбъект.СрокКредита, ЭтотОбъект.СтавкаКредитования);
КонецПроцедуры

&НаКлиенте
Функция КоэффициентАннуитета(СрокКредитаДляРасчета, СтавкаКредитованияДляРасчета)
	Если СтавкаКредитования = 0
		Или СрокКредитаДляРасчета = 0 Тогда
		Возврат 0;
	КонецЕсли; 
	СтавкаКредитованияСотые = СтавкаКредитования/100/12;
	Показатель1 = Pow(1 + СтавкаКредитованияСотые, СрокКредитаДляРасчета);
	Возврат СтавкаКредитованияСотые * Показатель1 / (Показатель1 - 1);
КонецФункции

&НаКлиенте
Процедура ПерезаполнитьРасчетнуюТаблицу()
	
	// Ничего более прямого не придумал
	Если ЭтотОбъект.РасчетнаяТаблица.Количество() > ЭтотОбъект.СрокКредита Тогда
		КоличествоУдалить = ЭтотОбъект.РасчетнаяТаблица.Количество() - ЭтотОбъект.СрокКредита;
		Для Сч = 1 По КоличествоУдалить Цикл
			РасчетнаяТаблица.Удалить(ЭтотОбъект.РасчетнаяТаблица.Количество() - 1);
		КонецЦикла;
	ИначеЕсли ЭтотОбъект.РасчетнаяТаблица.Количество() < ЭтотОбъект.СрокКредита Тогда
		КоличествоДобавить =  ЭтотОбъект.СрокКредита - ЭтотОбъект.РасчетнаяТаблица.Количество();
		Для Сч = 1 По КоличествоДобавить Цикл
			НоваяСтрока = ЭтотОбъект.РасчетнаяТаблица.Добавить();
			НоваяСтрока.НомерПериода = ЭтотОбъект.РасчетнаяТаблица.Количество();
		КонецЦикла;
	КонецЕсли;
	
	Если ЭтотОбъект.СуммаКредита = 0 или ЭтотОбъект.СрокКредита = 0 Или ЭтотОбъект.СтавкаКредитования = 0 Тогда
		Возврат;
	КонецЕсли; 
	
	ОстатокОД = ЭтотОбъект.СуммаКредита;
	
	
	
	ЕжемесячнаяСтавка = ЭтотОбъект.СтавкаКредитования / 12 / 100;
	// Расчет аннуитетного платежа
	СуммаЕжемесячногоПлатежа = 	СуммаЕжемесячногоПлатежа(Этотобъект.СуммаКредита, ЕжемесячнаяСтавка, СрокКредита);

	
	
	Для Каждого СтрокаТЗ Из ЭтотОбъект.РасчетнаяТаблица Цикл
		РасчитатьПоказателиСтроки_ДопВУменьшениеПлатежа(СтрокаТЗ, СуммаЕжемесячногоПлатежа, СуммаЕжемесячногоПлатежа + СтрокаТЗ.СуммаДопПлатежа, ОстатокОД, ЕжемесячнаяСтавка);
		ОстатокОД = СтрокаТЗ.ОстатокКУплатеОД;
	КонецЦикла;
	
	ПересчитатьИтоговыеПоказатели();
	
КонецПроцедуры


&НаКлиенте
Функция СуммаЕжемесячногоПлатежа(ОстатокОД, ЕжемесячнаяСтавка, СрокКредита)
	
	Если СрокКредита = 0 Тогда
		Возврат 0;
	КонецЕсли;
	
	Возврат ОстатокОД / (1-1/pow((1 + ЕжемесячнаяСтавка), СрокКредита)) * ЕжемесячнаяСтавка;

КонецФункции

&НаКлиенте
Процедура СуммаКредитаПриИзменении(Элемент)
	ОчиститьДанныеДопПлатежей();
	ПерезаполнитьРасчетнуюТаблицу();
КонецПроцедуры

&НаКлиенте 
Процедура ПересчитатьИтоговыеПоказатели()
	
	ЭтотОбъект.ПогашениеОДИтого = ЭтотОбъект.РасчетнаяТаблица.Итог("СуммаПогашенияОД");
	ЭтотОбъект.ПогашениеПроцентовИтого = ЭтотОбъект.РасчетнаяТаблица.Итог("СуммаПогашенияПроцентов");
	ЭтотОбъект.ДопПлатежИтого = ЭтотОбъект.РасчетнаяТаблица.Итог("СуммаДопПлатежа");
	ЭтотОбъект.ВсегоВыплачено = ЭтотОбъект.ПогашениеОДИтого + ЭтотОбъект.ПогашениеПроцентовИтого + ЭтотОбъект.ДопПлатежИтого;
	
	ЭтотОбъект.Альт_СуммаСбереженийЗаМесяц = ЭтотОбъект.СуммаКредита / ЭтотОбъект.СрокКредита;
	ЭтотОбъект.Альт_СуммаКоммуналки = 5000;
	ЭтотОбъект.Альт_МаксимальнаяСуммаАренды = (ЭтотОбъект.ПогашениеОДИтого + ЭтотОбъект.ПогашениеПроцентовИтого ) / ЭтотОбъект.СрокКредита - ЭтотОбъект.Альт_СуммаСбереженийЗаМесяц + ЭтотОбъект.Альт_СуммаКоммуналки;
	
КонецПроцедуры

&НаКлиенте 
Процедура ПересчитатьИтоговыеПоказателиПоСтроке(СтрокаТЗ, ОстатокОД)
	
	СтрокаТЗ.ОстатокКУплатеОД 			= ОстатокОД - СтрокаТЗ.СуммаПогашенияОД - СтрокаТЗ.СуммаДопПлатежа;
	СтрокаТЗ.ПогашениеОДИтого = СтрокаТЗ.СуммаПогашенияОД + СтрокаТЗ.СуммаДопПлатежа;
	СтрокаТЗ.МесячныйПлатежПлюсДоп = СтрокаТЗ.СуммаПлатежа + СтрокаТЗ.СуммаДопПлатежа;
		
КонецПроцедуры

&НаКлиенте
Процедура РасчетнаяТаблицаСуммаДопПлатежаПриИзменении(Элемент)
	ПерезаполнитьРасчетнуюТаблицу();
КонецПроцедуры

&НаКлиенте
Процедура РасчитатьПлатежиВУменьшениеПлатежа(Команда)
	
	ОчиститьДанныеДопПлатежей();
	
	Если ЭтотОбъект.РасчетнаяТаблица.Количество() = 0 Тогда
		Возврат;
	КонецЕсли; 
	
	СуммаЕжемесячногоПлатежа = ЭтотОбъект.РасчетнаяТаблица[0].СуммаПлатежа;
	СуммаЕжемесячногоПлатежаНачальная = СуммаЕжемесячногоПлатежа;
	СуммаЕжемесПлатежаПлюсДоп = СуммаЕжемесячногоПлатежаНачальная + расчет_ДопПлатеж;
	
	ОстатокОД = ЭтотОбъект.СуммаКредита;
	ЕжемесячнаяСтавка = ЭтотОбъект.СтавкаКредитования / 12 / 100;
	
	Для Каждого СтрокаТЗ Из ЭтотОбъект.РасчетнаяТаблица Цикл
		
		РасчитатьПоказателиСтроки_ДопВУменьшениеПлатежа(СтрокаТЗ, СуммаЕжемесячногоПлатежа, СуммаЕжемесПлатежаПлюсДоп, ОстатокОД, ЕжемесячнаяСтавка);
		
		ОстатокОД = СтрокаТЗ.ОстатокКУплатеОД;
		
	КонецЦикла;
	
	ПересчитатьИтоговыеПоказатели();
	
КонецПроцедуры

&НаКлиенте
Процедура РасчитатьПоказателиСтроки_ДопВУменьшениеПлатежа(СтрокаТЗ, СуммаЕжемесячногоПлатежа, СуммаЕжемесПлатежаПлюсДоп, ОстатокОД, ЕжемесячнаяСтавка)
	
		Если ОстатокОД < 0  Тогда
			Возврат;
		КонецЕсли;
		
		СтрокаТЗ.СуммаПлатежа 				= СуммаЕжемесячногоПлатежа;
		СтрокаТЗ.СуммаПогашенияПроцентов 	= ОстатокОД * ЕжемесячнаяСтавка;
		СтрокаТЗ.СуммаПогашенияОД 			= СтрокаТЗ.СуммаПлатежа - СтрокаТЗ.СуммаПогашенияПроцентов;
		
		Если ОстатокОД < СуммаЕжемесПлатежаПлюсДоп - СтрокаТЗ.СуммаПогашенияПроцентов
			И СтрокаТЗ.НомерПериода >= расчет_ПервыйМесяцДопПлатежа Тогда
			СтрокаТЗ.СуммаДопПлатежа = ОстатокОД  - СтрокаТЗ.СуммаПогашенияОД;
		ИначеЕсли СтрокаТЗ.НомерПериода >= расчет_ПервыйМесяцДопПлатежа Тогда
			СтрокаТЗ.СуммаДопПлатежа = СуммаЕжемесПлатежаПлюсДоп - СуммаЕжемесячногоПлатежа;
		КонецЕсли;
		
		// Итоговые показатели периода
		ПересчитатьИтоговыеПоказателиПоСтроке(СтрокаТЗ, ОстатокОД);
		
		Если СтрокаТЗ.СуммаДопПлатежа > 0 Тогда
			СуммаЕжемесячногоПлатежа = СуммаЕжемесячногоПлатежа(СтрокаТЗ.ОстатокКУплатеОД, ЕжемесячнаяСтавка, ЭтотОбъект.СрокКредита - СтрокаТЗ.НомерПериода);
		КонецЕсли;
		
		
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПараметрыПоУмолчанию(Команда)
	
	ЭтотОбъект.СрокКредита = 300;
	ЭтотОбъект.СтавкаКредитования = 5.99;
	ЭтотОбъект.СуммаКредита = 7122000;
	ЭтотОбъект.расчет_ДопПлатеж = 19000;
	
	ЭтотОбъект.РасчетнаяТаблица.Очистить();
	
	ПерезаполнитьРасчетнуюТаблицу();
	ПересчитатьИтоговыеПоказатели();
КонецПроцедуры


&НаКлиенте
Процедура ОчиститьДанныеДопПлатежей()

	  Для Каждого СтрокаТЗ Из ЭтотОбъект.РасчетнаяТаблица Цикл
		СтрокаТЗ.СуммаДопПлатежа = 0;
	КонецЦикла;
	
	ПересчитатьИтоговыеПоказатели();

КонецПроцедуры

&НаКлиенте
Процедура РасчитатьПлатежиВУменьшениеСрока(Команда)
	
	ОчиститьДанныеДопПлатежей();
	
	Если ЭтотОбъект.РасчетнаяТаблица.Количество() = 0 Тогда
		Возврат;
	КонецЕсли; 
	
	СуммаЕжемесячногоПлатежа = ЭтотОбъект.РасчетнаяТаблица[0].СуммаПлатежа;
	ОстатокОД = ЭтотОбъект.СуммаКредита;
	
	ЕжемесячнаяСтавка = ЭтотОбъект.СтавкаКредитования / 12 / 100;
	
	ОчищатьДанныеПлатежей = Ложь;
	
	
	
	Для Каждого СтрокаТЗ Из ЭтотОбъект.РасчетнаяТаблица Цикл

		Если ОчищатьДанныеПлатежей Тогда
			СтрокаТЗ.СуммаДопПлатежа = 0;
			СтрокаТЗ.СуммаПлатежа = 0;
			СтрокаТЗ.СуммаПогашенияОД = 0;
			СтрокаТЗ.СуммаПогашенияПроцентов = 0;
			СтрокаТЗ.ОстатокКУплатеОД = 0;
			Продолжить;
		КонецЕсли;
		
		СтрокаТЗ.СуммаПлатежа = Мин(СуммаЕжемесячногоПлатежа, ОстатокОД);
		СтрокаТЗ.СуммаПогашенияПроцентов = ОстатокОД* ЕжемесячнаяСтавка;
		СтрокаТЗ.СуммаПогашенияОД = Мин(СтрокаТЗ.СуммаПлатежа - СтрокаТЗ.СуммаПогашенияПроцентов, СуммаЕжемесячногоПлатежа);
	
		СтрокаТЗ.СуммаДопПлатежа = Мин(ОстатокОД - СтрокаТЗ.СуммаПогашенияОД, расчет_ДопПлатеж);
		
		СтрокаТЗ.ОстатокКУплатеОД = ОстатокОД - СтрокаТЗ.СуммаПогашенияОД - СтрокаТЗ.СуммаДопПлатежа;
		ОстатокОД = СтрокаТЗ.ОстатокКУплатеОД;
		ОчищатьДанныеПлатежей = ОчищатьДанныеПлатежей Или СтрокаТЗ.СуммаПогашенияОД >= СуммаЕжемесячногоПлатежа;
	
	КонецЦикла;
	
	ПересчитатьИтоговыеПоказатели();

КонецПроцедуры
